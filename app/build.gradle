apply plugin: 'com.android.application'

apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "com.jnhyxx.html5"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        renderscriptTargetApi 22
        renderscriptSupportModeEnabled true

        multiDexEnabled true

        // the ndk and manifest configs are for getui
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "mips", "mips64", "x86", "x86_64"
        }

        manifestPlaceholders = [
                GETUI_APP_ID    : "gnUGtVngQGA80LcPdfbHj9",
                GETUI_APP_KEY   : "LwJyVosAAu5WcdtMsWQSx6",
                GETUI_APP_SECRET: "QDYKD2qJoGAk9O5DfMicg3",
                PACKAGE_NAME    : applicationId
        ]
    }

    java.util.Properties properties = new Properties();
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.get('keystore_storeFile'))
            storePassword properties.get('keystore_storePassword')
            keyAlias properties.get('keystore_keyAlias')
            keyPassword properties.get('keystore_keyPassword')
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-umeng.txt'
            signingConfig signingConfigs.release
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def newFileName = file.name.replace(".apk", "-${variant.versionName}.apk")
            output.outputFile = new File(file.parent, newFileName)
        }
    }
    productFlavors {
        origin {
            versionCode 12
            versionName '1.2.12'
            buildConfigField('String', 'API_HOST', '\"http://newtest.jnhyxx.com\"')
//            minSdkVersion 21
        }
        yybd { // 原油宝典 -> 期货交易通
            applicationId 'com.jnhyxx.yybd'
            versionCode 6
            versionName '1.1.2'
            buildConfigField('String', 'API_HOST', '\"http://new.jnhyxx.com\"')

            manifestPlaceholders = [
                    GETUI_APP_ID    : "zDqB744dvm5uKOw5X4LUh5",
                    GETUI_APP_KEY   : "RdPe1ldvUp8CD6YvsuI218",
                    GETUI_APP_SECRET: "2ihqKbsxit8PolJhVcEMD",
                    PACKAGE_NAME    : applicationId
            ]
        }

        ryt {
            applicationIdSuffix '.ryt'
            versionCode 1
            versionName '1.0'
            buildConfigField('String', 'API_HOST', '\"http://www.rytqh.com\"')
        }
        ycp {
            applicationIdSuffix '.ycp'
            versionCode 1
            versionName '1.0'
            buildConfigField('String', 'API_HOST', '\"http://ycp.jnhyxx.com\"')
        }
    }

    dexOptions {
        javaMaxHeapSize '4g'
    }

    packagingOptions {
        exclude "META-INF/INDEX.LIST" // will not include INDEX file
        exclude "META-INF/io.netty.versions.properties"
        // for getui
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    originCompile project(path: ':umenglibrary', configuration: 'originRelease')
    yybdCompile project(path: ':umenglibrary', configuration: 'yybdRelease')
    ycpCompile project(path: ':umenglibrary', configuration: 'ycpRelease')
    rytCompile project(path: ':umenglibrary', configuration: 'rytRelease')
    compile project(':SlidingMenu')
    compile project(':kutils')
    compile project(':chart')
    compile project(':livevideo')
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'
    compile 'com.android.support:multidex:1.0.0'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile files('libs/netty-buffer-4.1.5.final.jar')
    compile files('libs/netty-codec-4.1.5.final.jar')
    compile files('libs/netty-common-4.1.5.final.jar')
    compile files('libs/netty-transport-4.1.5.final.jar')
    compile files('libs/netty-resolver-4.1.5.final.jar')
    compile 'com.getui:sdk:2.9.3.0'
}
