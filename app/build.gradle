apply plugin: 'com.android.application'

apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "com.jnhyxx.html5"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        buildConfigField('String', 'API_HOST', '\"http://www.jnhyxx.com\"')

        renderscriptTargetApi 22
        renderscriptSupportModeEnabled true

        multiDexEnabled true

        // todo 加入此行代码，防止出现 Error:Execution failed for task ':app:transformResourcesWithMergeJavaResForMicroilDebug'.
        // com.android.build.api.transform.TransformException: com.android.builder.packaging.DuplicateFileException: Duplicate files copied in APK META-INF/INDEX.LIST
        packagingOptions {
            exclude "META-INF/INDEX.LIST" // will not include INDEX file
            exclude "META-INF/io.netty.versions.properties" // will not include INDEX file
        }
    }

    java.util.Properties properties = new Properties();
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.get('keystore_storeFile'))
            storePassword properties.get('keystore_storePassword')
            keyAlias properties.get('keystore_keyAlias')
            keyPassword properties.get('keystore_keyPassword')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-umeng.txt'
            signingConfig signingConfigs.release
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def newFileName = file.name.replace(".apk", "-${variant.versionName}.apk")
            output.outputFile = new File(file.parent, newFileName)
        }
    }
    productFlavors {
        origin {
            versionCode 6
            versionName '1.2.1'
        }
        app1 {
            versionCode 1
            versionName '1.0'
            buildConfigField('String', 'API_HOST', '\"http://app1.jnhyxx.com\"')
        }
        microil {
            versionCode 1
            versionName '1.0.0'
            buildConfigField('String', 'API_HOST', '\"http://newtest.jnhyxx.com\"')
            minSdkVersion 21
        }
        tst {
            applicationId 'com.jnhyxx.test'
            versionCode 1
            versionName '1.0'
            buildConfigField('String', 'API_HOST', '\"http://139.129.205.244\"')
        }
        ryt {
            applicationIdSuffix '.ryt'
            versionCode 1
            versionName '1.0'
            buildConfigField('String', 'API_HOST', '\"http://www.rytqh.com\"')
        }
        ycp {
            applicationIdSuffix '.ycp'
            versionCode 1
            versionName '1.0'
            buildConfigField('String', 'API_HOST', '\"http://ycp.jnhyxx.com\"')
        }
    }

    dexOptions {
        javaMaxHeapSize '4g'
    }
}

dependencies {
    app1Compile files('libs/via_sdk_v1.jar')
    provided files('libs/via_sdk_v1.jar')
    microilCompile project(path: ':umenglibrary', configuration: 'microilRelease')
    originCompile project(path: ':umenglibrary', configuration: 'originRelease')
    app1Compile project(path: ':umenglibrary', configuration: 'originRelease')
    tstCompile project(path: ':umenglibrary', configuration: 'tstRelease')
    ycpCompile project(path: ':umenglibrary', configuration: 'ycpRelease')
    rytCompile project(path: ':umenglibrary', configuration: 'rytRelease')
    compile project(':PushSDK')
    compile project(':SlidingMenu')
    compile project(':kutils')
    compile project(':chart')
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'
    compile 'com.android.support:multidex:1.0.0'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile files('libs/netty-buffer-4.1.5.final.jar')
    compile files('libs/netty-codec-4.1.5.final.jar')
    compile files('libs/netty-common-4.1.5.final.jar')
    compile files('libs/netty-transport-4.1.5.final.jar')
}
